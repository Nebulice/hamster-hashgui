plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.2.1'
}

group = 'nebulice.hamster.hashGui'
version = 'R0.2.0-SNAPSHOT'

def hashrc = '/home/blegourr/dev/nebulice/Hamster/PLUGIN/libs/hamster-hashgui/build_plugin'
def libsFolder = 'libs'

repositories {
    mavenCentral()
    maven {
        name = "papermc"
        url = uri("https://repo.papermc.io/repository/maven-public/")
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "minecraft-repo"
        url = "https://libraries.minecraft.net/"
    }
}

configurations {
    implementation.canBeResolved = true
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.21.10-R0.1-SNAPSHOT"
    compileOnly 'com.mojang:authlib:1.5.21'

    implementation fileTree(dir: libsFolder, include: ['*.jar'])
}

def targetJavaVersion = 21

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}
tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
    options.release.set(targetJavaVersion)
}

processResources {
    def props = [
            name   : rootProject.name,
            version: version
    ]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    dependencies {
        exclude(dependency('io.papermc.paper:paper-api:1.21.10-R0.1-SNAPSHOT'))
        exclude(dependency('com.mojang:authlib:1.5.21'))
    }

    archiveClassifier.set('')

    mergeServiceFiles()
    minimize()
}

tasks.register('make', Copy) {
    dependsOn shadowJar

    from('build/libs')
    into hashrc
    include '*.jar'
}
